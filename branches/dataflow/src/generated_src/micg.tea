{-
 - phc -- the open source PHP compiler
 - See doc/license/README.license for licensing information
 -
 - Definition of the structure for the Macro-Inline Code Generator.
 -
 -}

{-
	Configuration
-}

output_dir "src/generated";
prefix "MICG";
external class "Object";
external class "String";
use list "List";
use string "String";
use namespace "MICG";
no source_rep;

{-
   Top-level structure	
-}

-- We dont need this, we just need Node_factory to create a Macro_list.
All ::= Macro* ; 

Macro ::= Signature Rule* Body ;

-- Signature
Signature ::= MACRO_NAME Formal_parameter* ;
Formal_parameter ::= TYPE_NAME PARAM_NAME ;

-- Rules

Rule ::= Equals | Lookup ;

Lookup ::= PARAM_NAME ATTR_NAME ;

Equals ::= left:Expr right:Expr ;
Expr ::= Lookup | PARAM_NAME | STRING ;

-- Body
Body ::= Body_part* ;
Body_part ::= C_CODE | Macro_call | Interpolation ;

Macro_call ::= MACRO_NAME Actual_parameter*;
Actual_parameter ::= STRING | PARAM_NAME ;

Interpolation ::= Lookup | PARAM_NAME ;


{-
 - Extra attributes and methods (mixin code)
 -}

#include <iostream>
#include <sstream>
#include <iomanip>
#include "boost/lexical_cast.hpp"
#include "lib/error.h"
#include "lib/Object.h"
#include "lib/List.h"
#include "lib/String.h"
#include "lib/Boolean.h"
#include "lib/Integer.h"

class Node : Object
{
};

